#!/usr/bin/env ocaml
;;

#load "unix.cma";;

let query ?env ~default cmd =
  let run_git () =
    try
      let chan = Unix.open_process_in cmd in
      let out = input_line chan in
      if Unix.close_process_in chan = Unix.WEXITED 0 then out
      else default
    with End_of_file -> default

  in
    match env with
    |None -> run_git ()
    |Some env -> try Sys.getenv env with Not_found -> run_git ()

let hash = query ~env:"GIT_SHORTREF" ~default:"unknown" "git show -s --pretty=format:%H"

let date = query ~env:"GIT_DATETIME" ~default:"not-available" "git show -s --pretty=format:%ci"

(* find the most recent tag. If one commit is associated with two or more tags,
   output always the most recently added tag *)
let git_describe = query ~env:"GIT_VERSION" ~default:"dev" "git describe --tags"

let () =
  Format.printf
    "@[<v>let commit_hash = \"%s\"@,let committer_date = \"%s\"@,let git_describe = \"%s\"@]@."
    hash
    date
    git_describe
