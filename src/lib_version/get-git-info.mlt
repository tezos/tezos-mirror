#!/usr/bin/env ocaml
;;
#load "unix.cma"

let v ?(patch=0) ?(extra="") major minor = (major, minor, patch, extra)

let parse_version s =
  try
    try
      Scanf.sscanf s "v%d.%d.%d-%s" (fun major minor patch extra ->
          v ~patch ~extra major minor)
    with End_of_file | Scanf.Scan_failure _ -> (
      try
        Scanf.sscanf s "v%d.%d-%s" (fun major minor extra -> v ~extra major minor)
      with End_of_file | Scanf.Scan_failure _ -> (
        try
          Scanf.sscanf s "v%d.%d.%d" (fun major minor patch ->
              v ~patch major minor)
        with End_of_file | Scanf.Scan_failure _ ->
          Scanf.sscanf s "v%d.%d" (fun major minor -> v major minor)))
  with End_of_file | Scanf.Scan_failure _ -> v ~extra:"dev" 0 0

let parse_extra s =
  try Scanf.sscanf s "rc%d" (fun d -> ("rc", d))
  with
  | End_of_file -> ("",0)
  | Scanf.Scan_failure _ -> ("dev",0)

let query ?env ~default cmd =
  let default () =
    match env with
    |None -> default
    |Some env -> try Sys.getenv env with Not_found -> default
  in
  try
    let chan = Unix.open_process_in cmd in
    let out = input_line chan in
    if Unix.close_process_in chan = Unix.WEXITED 0 then out
    else default ()
  with End_of_file -> default ()

let hash = query ~env:"GIT_SHORTREF" ~default:"unknown" "git show -s --pretty=format:%H"

let date = query ~env:"GIT_DATETIME" ~default:"not-available" "git show -s --pretty=format:%ci"

(* find the most recent tag. If one commit is associated with two or more tags,
   output always the most recently added tag *)
let version =
  parse_version (query ~env:"GIT_VERSION" ~default:"dev" "git describe --tags")

let () =
  let (major, minor, patch, extra) = version in
  let (s,n) = parse_extra extra in
  Format.printf
    "@[<v>let commit_hash = \"%s\"@,let committer_date = \"%s\"@,let tag_version = (%d,%d,%d,(\"%s\",%d))@]@."
    hash
    date
    major
    minor
    patch
    s
    n
